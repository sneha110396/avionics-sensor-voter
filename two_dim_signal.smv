-- Module realWorld will generate signal values between a given range
-- Signal is two dimensional
-- Signal will be an array with two component
 

MODULE realWorld                    

DEFINE
	MAX_VAL:= 5;		  -- maximum value of the signal
	MIN_VAL:= -5;		  -- minimum value of the signal

VAR
	out : array 1..2 of MIN_VAL..MAX_VAL;   -- generating two dimensional signal values
						-- each component takes a random value between MIN_VAL and MAX_VAL

-- In each step, each component of out can increase by 1, decrease by 1 or stay same
ASSIGN 
	
	next(out[1]) := case
			out[1] < MAX_VAL & out[1] >MIN_VAL : {out[1]-1, out[1], out[1]+1};
			out[1] = MAX_VAL : {out[1], out[1]-1};
			out[1] = MIN_VAL : {out[1], out[1]+1};
			esac;
	next(out[2]) := case
			out[2] < MAX_VAL & out[2] >MIN_VAL : {out[2]-1, out[2], out[2]+1};
			out[2] = MAX_VAL : {out[2], out[2]-1};
			out[2] = MIN_VAL : {out[2], out[2]+1};
			esac;

-----------------------------------------------------------------------------------------------------------------------------------------------------

-- Module sensor will create sensors which will measure the signal values created by realWorld Module
-- It takes the realWorld module as input
-- Each sensor will have a noise. Each sensor can have three types of "fault"
-- Each sensor will output a signal value and a hardware flag
-- hardware flag of each sensor indicates if there is a hardware fault in the sensor or not

MODULE sensor(world)

VAR
	hw_valid: boolean;         		-- TRUE means hardware flag is indicating valid, FALSE otherwise
	
	fault: 0..2;               		-- 0 means no fault
	                           		-- 1 means signal is an arbitrary value
				   		-- 2 means hw_valid may not be correct
				   
	noise: array 1..2 of -1..1;             -- noise is an array with two component. Each component can be between -1 to 1
	
	signal: array 1..2 of (world.MIN_VAL) .. (world.MAX_VAL); 
						-- signal is a two dimansional array
	
	

ASSIGN
	-- intially fault is 0
	init(fault) := 0;                                 
	-- each step it will take arbitrary value between 0 and 2
	
				
-- hw_valid will take any arbitraey boolean value
-- If fault = 0, hw_valid will be TRUE
	
INVAR (fault=0 -> hw_valid=TRUE)

-- signal can take any arbitrary value between MIN_VAL and MAX_VAL
-- If fault is 0, signal value will be componentwise sum of realWorld value and noise
-- If any component of signal value exceeds the MAX_VAL, we will set it as MAX_VAL
-- If any component of signal value becomes less than MIN_VAL, we will set it as MIN_VAL
-- If fault is not 0, both components of signal can take any arbitrary value between MAX_VAL and MIN_VAL
	

INVAR (fault=0 & (abs(world.out[1] + noise[1]) <=world.MAX_VAL)) -> (signal[1] = world.out[1] + noise[1])
INVAR (fault=0 & (world.out[1] + noise[1]) > world.MAX_VAL) -> (signal[1] = world.MAX_VAL)
INVAR (fault=0 & (world.out[1] + noise[1]) < world.MIN_VAL) -> (signal[1] = world.MIN_VAL)

INVAR (fault=0 & (abs(world.out[2] + noise[2]) <=world.MAX_VAL)) -> (signal[2] = world.out[2] + noise[2])
INVAR (fault=0 & (world.out[2] + noise[2]) > world.MAX_VAL) -> (signal[2] = world.MAX_VAL)
INVAR (fault=0 & (world.out[2] + noise[2]) < world.MIN_VAL) -> (signal[2] = world.MIN_VAL)

------------------------------------------------------------------------------------------------------------------------------------------------------


-- Main module will work as the voter here

MODULE main               			       

DEFINE
	HW_PERSISTENCE := 3;                           -- hardware persistence 
	MC_PERSISTENCE := 3;                           -- miscompare persistence
    	MC_val_threshold := 2;                         -- two sensor miscompare if their signal value differ more than this threshold

VAR
   	rWorld : realWorld();			       -- Instance of the realWorld module
	
	sensor1 : sensor(rWorld);		       -- First Instance of sensor module
	sensor2 : sensor(rWorld);		       -- Second instance of sensor module
	sensor3 : sensor(rWorld);		       -- Third instance of sensor module
	
	hw_count: array 1..3 of 0..HW_PERSISTENCE;     -- variable to measure hardware error persistence
	
	mc_count: array 1..3 of 0..MC_PERSISTENCE;     -- variable to measure miscompare persistence

	isolated: array 1..3 of boolean;               -- TRUE if sensors are isolated
	
	landmark : {"reset", "thres_hit"};             -- variable to indicate if the miscompare variable reached threshold or did reset
	

ASSIGN 

	-- Initially, value of isolated is FALSE for all sensors
	
	init(isolated[1]) := FALSE;
	init(isolated[2]) := FALSE;
	init(isolated[3]) := FALSE;
	
	-- If hardware count of a sensor reaches hardware persisitence, sensor will be isolated
	-- If number of active sensors is three and miscopmpare count of that sensor reaches miscompare persistence, sensor will be isolated
	-- isolated will be TRUE is the corresponding sensor is isolated
	
	next(isolated[1]) := case
				(hw_count[1]=HW_PERSISTENCE) | (numActive=3 & mc_count[1]=MC_PERSISTENCE) : TRUE;
				isolated[1] : TRUE;
				TRUE : FALSE;
				esac;
				
	next(isolated[2]) := case
				(hw_count[2]=HW_PERSISTENCE) | (numActive=3 & mc_count[2]=MC_PERSISTENCE) : TRUE;
				isolated[2] : TRUE;
				TRUE : FALSE;
				esac;
				
	next(isolated[3]) := case
				(hw_count[3]=HW_PERSISTENCE) | (numActive=3 & mc_count[3]=MC_PERSISTENCE) : TRUE;
				isolated[3] : TRUE;
				TRUE : FALSE;
				esac;
				
	-- Value of hardware count is zero initially for all sensors
	
	init(hw_count[1]) := 0;
	init(hw_count[2]) := 0;
	init(hw_count[3]) := 0;
	
	-- If hardware flag of some sensor if FALSE, then hardware count will increase by one, provided it is less than the persistence level
	
	next(hw_count[1]) := case
				(next(sensor1.hw_valid) = FALSE) & (hw_count[1]<HW_PERSISTENCE) : hw_count[1]+1;
				next(sensor1.hw_valid) = TRUE : 0;
				hw_count[1]=HW_PERSISTENCE : HW_PERSISTENCE;
				TRUE : hw_count[1];
				esac;
	
	next(hw_count[2]) := case
				(next(sensor2.hw_valid) = FALSE) & (hw_count[2]<HW_PERSISTENCE) : hw_count[2]+1;
				next(sensor2.hw_valid) = TRUE : 0;
				hw_count[2]=HW_PERSISTENCE : HW_PERSISTENCE;
				TRUE : hw_count[2];
				esac;
	
	next(hw_count[3]) := case
				(next(sensor3.hw_valid) = FALSE) & (hw_count[3]<HW_PERSISTENCE) : hw_count[3]+1;
				next(sensor3.hw_valid) = TRUE : 0;
				hw_count[3]=HW_PERSISTENCE : HW_PERSISTENCE;
				TRUE : hw_count[3];
				esac;
				
	-- Miscompare count will be zero initially for all the sensors
	
	init(mc_count[1]) := 0;
	init(mc_count[2]) := 0;
	init(mc_count[3]) := 0;
	
	-- When there are three active sensors, it will increase by one if the respective sensor is miscomparing
	-- When there are two active sensors and the respective sensor is isolated, it will count the miscompare between the other two sensors
	-- When the output is valid, it will increment it's value when there is a miscompare and and reset it's value if miscompare clears
	-- When output is not valid, it will increment if there is a miscompare, decrement otherwise
	
	next(mc_count[1]) := case
	                        -- three active sensors
				
				(next(numActive)=3) & next(miscompare12)=TRUE & next(miscompare31)=TRUE & (mc_count[1]<MC_PERSISTENCE) : mc_count[1] +1;
				(next(numActive)=3) & (next(miscompare12)=FALSE | next(miscompare31)=FALSE) & (mc_count[1]<MC_PERSISTENCE): 0;
				(numActive=3) & (mc_count[1]=MC_PERSISTENCE) : 0;
				
				-- two active sensors
				
				next(numActive)=2 & next(isolated[1])=TRUE & (next(miscompare23) =TRUE) & (mc_count[1] < MC_PERSISTENCE): mc_count[1]+1;
				next(numActive)=2 & next(isolated[1])=TRUE & next(landmark)= "reset" & (next(miscompare23) = FALSE) : 0;
				next(numActive)=2 & next(isolated[1])=TRUE & next(landmark)= "thres_hit" & (next(miscompare23) = FALSE) & (mc_count[1] >0) : mc_count[1] - 1;
				TRUE : mc_count[1];
				esac;
				
	next(mc_count[2]) := case
				-- three active sensors
				
				(next(numActive)=3) & next(miscompare12)=TRUE & next(miscompare23)=TRUE & (mc_count[2]<MC_PERSISTENCE) : mc_count[2] +1;
				(next(numActive)=3) & (next(miscompare12)=FALSE | next(miscompare23)=FALSE) & (mc_count[2]<MC_PERSISTENCE): 0;
				(numActive=3) & (mc_count[2]=MC_PERSISTENCE) : 0;

				-- two active sensors
				
				next(numActive)=2 & next(isolated[2])=TRUE & (next(miscompare31)=TRUE) & (mc_count[2] < MC_PERSISTENCE): mc_count[2]+1;
				next(numActive)=2 & next(isolated[2])=TRUE & next(landmark)="reset" & (next(miscompare31)=FALSE) : 0;
				next(numActive)=2 & next(isolated[2])=TRUE & next(landmark)="thres_hit" & (next(miscompare31)=FALSE) & (mc_count[2] >0) : mc_count[2] - 1;
				TRUE : mc_count[2];
				esac;
				
	next(mc_count[3]) := case
				--- three active sensors
				
				next(numActive)=3 & next(miscompare31)=TRUE & next(miscompare23)=TRUE & (mc_count[3]<MC_PERSISTENCE) : mc_count[3] +1;
				next(numActive)=3 & (next(miscompare31)=FALSE | next(miscompare23)=FALSE) & (mc_count[3]<MC_PERSISTENCE) : 0;
				(numActive=3) & (mc_count[3]=MC_PERSISTENCE) : 0;

				-- two active sensors
				
				next(numActive)=2 & next(isolated[3])=TRUE & (next(miscompare12)=TRUE) & (mc_count[3] < MC_PERSISTENCE): mc_count[3]+1;
				next(numActive)=2 & next(isolated[3])=TRUE & next(landmark)="reset" & (next(miscompare12)=FALSE) : 0;
				next(numActive)=2 & next(isolated[3])=TRUE & next(landmark)="thres_hit" & (next(miscompare12)=FALSE) & (mc_count[3] >0) : mc_count[3] - 1;
				TRUE : mc_count[3];
				esac;

	-- We need "landmark" when there are only two valid sensors
	-- landmark will take the value "reset", if the working miscompare count has done reset previously
	-- landmark will take the value "thres_hit" if the working miscompare count has reached the threshold previously
				
	init(landmark):= "reset";
	next(landmark):= case
						numActive=2 & isolated[1] & mc_count[1]=MC_PERSISTENCE : "thres_hit";
						numActive=2 & isolated[2] & mc_count[2]=MC_PERSISTENCE : "thres_hit";
						numActive=2 & isolated[3] & mc_count[3]=MC_PERSISTENCE : "thres_hit";
						numActive=2 & isolated[1] & mc_count[1]=0 : "reset";
						numActive=2 & isolated[2] & mc_count[2]=0 : "reset";
						numActive=2 & isolated[3] & mc_count[3]=0 : "reset";
						TRUE: landmark;
						esac;
				
							
DEFINE
	-- compute the absolute differences of each components of one sensor with the other sensor
	-- If any of them is greater than threshold, we will say the sensors are miscomapring
	
	-- miscomparison of sensor1 and sensor2
	miscompare12 :=  (abs(sensor1.signal[1] - sensor2.signal[1]) > MC_val_threshold) | (abs(sensor1.signal[2] - sensor2.signal[2]) > MC_val_threshold);
	
	-- miscomparison of sensor2 and sensor3
	miscompare23 :=  (abs(sensor2.signal[1] - sensor3.signal[1]) > MC_val_threshold) | (abs(sensor2.signal[2] - sensor3.signal[2]) > MC_val_threshold);
	
	-- miscomparison of sensor3 and sensor1
	miscompare31 :=  (abs(sensor3.signal[1] - sensor1.signal[1]) > MC_val_threshold) | (abs(sensor3.signal[2] - sensor1.signal[2]) > MC_val_threshold);

	-- Calculating number of active sensors at each step
	
	none_isolated := !isolated[1] & !isolated[2] & !isolated[3];
	one_isolated := (isolated[1] & !isolated[2] & !isolated[3]) | (!isolated[1] & isolated[2] & !isolated[3]) | (!isolated[1] & !isolated[2] & isolated[3]);
	two_isolated := (isolated[1] & isolated[2] & !isolated[3]) | (!isolated[1] & isolated[2] & isolated[3]) | (isolated[1] & !isolated[2] & isolated[3]);
	all_isolated := isolated[1] & isolated[2] & isolated[3];
	
	numActive := case
			none_isolated : 3;
			one_isolated : 2;
			two_isolated : 1;
			all_isolated : 0;
			esac;
			
	-- Output signal is the average value the signal values of all the active sensors
	-- outputSignal1 is the first component of the output signal
	-- outputSignal2 is the second component of the output signal
			
	outputSignal1 := case
			numActive=3 : (sensor1.signal[1] + sensor2.signal[1] + sensor3.signal[1])/3;
			numActive=2 & isolated[1]: (sensor2.signal[1] + sensor3.signal[1])/2;
			numActive=2 & isolated[2]: (sensor3.signal[1] + sensor1.signal[1])/2;
			numActive=2 & isolated[3]: (sensor1.signal[1] + sensor2.signal[1])/2;
			numActive=1 & isolated[1] & isolated[2] : sensor3.signal[1];
			numActive=1 & isolated[2] & isolated[3] : sensor1.signal[1];
			numActive=1 & isolated[3] & isolated[1] : sensor2.signal[1];
			numActive=0 : -100;
			esac;
			
	outputSignal2 := case
			numActive=3 : (sensor1.signal[2] + sensor2.signal[2] + sensor3.signal[2])/3;
			numActive=2 & isolated[1]: (sensor2.signal[2] + sensor3.signal[2])/2;
			numActive=2 & isolated[2]: (sensor3.signal[2] + sensor1.signal[2])/2;
			numActive=2 & isolated[3]: (sensor1.signal[2] + sensor2.signal[2])/2;
			numActive=1 & isolated[1] & isolated[2] : sensor3.signal[2];
			numActive=1 & isolated[2] & isolated[3] : sensor1.signal[2];
			numActive=1 & isolated[3] & isolated[1] : sensor2.signal[2];
			numActive=0 : -100;
			esac;
			
	-- When there are three active sensors, output will be valid
	-- When there are two active sensors and the corresponding miscompare count has reached threshold, then output is not valid
	-- If the miscompare count is zero, then the output is valid
	-- If the miscompare count is less than threshold and greater than zero, then output is valid if landmark is "reset", not valid otherwise
	-- If there are one active sensor, output is valid
	-- If there are no active sensors, output is not valid 

	outputValid := case 
						numActive=3 : TRUE;

						numActive=2 & isolated[1] & mc_count[1]=MC_PERSISTENCE : FALSE;
						numActive=2 & isolated[1] & mc_count[1]=0 : TRUE;
						numActive=2 & isolated[1] & mc_count[1] < MC_PERSISTENCE & mc_count[1] > 0 & landmark="thres_hit": FALSE;
						numActive=2 & isolated[1] & mc_count[1] < MC_PERSISTENCE & mc_count[1] > 0 & landmark="reset": TRUE;
				
						numActive=2 & isolated[2] & mc_count[2]=MC_PERSISTENCE : FALSE;
						numActive=2 & isolated[2] & mc_count[2]=0 : TRUE;
						numActive=2 & isolated[2] & mc_count[2] < MC_PERSISTENCE & mc_count[2] > 0 & landmark="thres_hit": FALSE;
						numActive=2 & isolated[2] & mc_count[2] < MC_PERSISTENCE & mc_count[2] > 0 & landmark="reset": TRUE;
						
						numActive=2 & isolated[3] & mc_count[3]=MC_PERSISTENCE : FALSE;
						numActive=2 & isolated[3] & mc_count[3]=0 : TRUE;
						numActive=2 & isolated[3] & mc_count[3] < MC_PERSISTENCE & mc_count[3] > 0 & landmark="thres_hit": FALSE;
						numActive=2 & isolated[3] & mc_count[3] < MC_PERSISTENCE & mc_count[3] > 0 & landmark="reset": TRUE;
						
						numActive=1 : TRUE;
						numActive=0 : FALSE;
						esac;
	
	-- If a sensor is not isolated, then its hw_count is less than threshold (required to describe a property)

	isolt_or_count_lt_persist := (!isolated[1] -> hw_count[1] < HW_PERSISTENCE) & (!isolated[2] -> hw_count[2] < HW_PERSISTENCE) & (!isolated[3] -> hw_count[3] < HW_PERSISTENCE);
	
	-- hw_count of one of the sensors reaches the persistence

	hw_fault := (hw_count[1] = HW_PERSISTENCE) | (hw_count[2] = HW_PERSISTENCE) | (hw_count[3] = HW_PERSISTENCE); 

	-- mc_count of one sensor reaches persistence

	mc_fault := (mc_count[1] = MC_PERSISTENCE) | (mc_count[2] = MC_PERSISTENCE) | (mc_count[3] = MC_PERSISTENCE);

--Invariants	

INVAR !((isolated[1]=FALSE & sensor1.fault!=0) & (isolated[2]=FALSE & sensor2.fault!=0)) 
INVAR !((isolated[1]=FALSE & sensor1.fault!=0) & (isolated[3]=FALSE & sensor3.fault!=0))
INVAR !((isolated[2]=FALSE & sensor2.fault!=0) & (isolated[3]=FALSE & sensor3.fault!=0))


--properties to check

-- If the system has reached numActive=2, it can not go back to numActive=3. Similarly for numActive=1 and 0

--LTLSPEC G(numActive=2-> (G(numActive!=3)))
--LTLSPEC G(numActive=1-> (G(numActive!=2)))
--LTLSPEC G(numActive=0-> (G(numActive!=1)))

-- If the system is in numActive 3, it is possible to reach numActive 2 and so on
 
CTLSPEC AG(numActive=3 -> EF(numActive=2))
CTLSPEC AG(numActive=2 -> EF(numActive=1))
CTLSPEC AG(numActive=1-> EF(numActive=0))

-- If the system is in numActive 2 and output is valid and all active sensors has hw_count less than threshold, then it can reach numActive 2 and output not valid
-- Same for output not valid to outputvalid

CTLSPEC AG((numActive=2 & outputValid & isolt_or_count_lt_persist) -> EF(numActive=2 & !outputValid))
CTLSPEC AG((numActive=2 & !outputValid & isolt_or_count_lt_persist) -> EF(numActive=2 & outputValid))

-- If numActive is three and one sensor's miscompare count reaches persistence, then it will reach numActive 2

CTLSPEC AG((numActive=3 & mc_fault) -> AF(numActive=2))
--LTLSPEC G((numActive=3 & mc_fault) -> F(numActive=2))

-- If numActive is three and one sensor's hardware fault reaches persistence, then it will reach numActive 2

CTLSPEC AG((numActive=3 & hw_fault) -> AF(numActive=2))
--LTLSPEC G((numActive=3 & hw_fault) -> F(numActive=2))

--CTLSPEC AG((outputSignal= rWorld.out) -> AF(A[(outputSignal = rWorld.out) U ((outputSignal= rWorld.out) & outputValid)] | (outputSignal = rWorld.out)))
